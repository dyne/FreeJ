dnl ==============================================================
dnl Process this file with autoconf to produce a configure script.
dnl ==============================================================

AC_INIT(src/freej.cpp)

AC_CANONICAL_HOST
AC_CANONICAL_TARGET

dnl ==============================================================
dnl Get the operating system and version number...
dnl ==============================================================
uname=`uname`
uversion=`uname -r | sed -e 's/-.*$//g;s/[\.]//g'`
AC_MSG_CHECKING(for which platform we are compiling)
case $uname in
     Linux)
     AC_MSG_RESULT(Linux)
     AC_DEFINE(HAVE_LINUX,1,[define if compiling for Linux])
     have_linux=yes
     ;;
     Darwin)
     AC_MSG_RESULT(Darwin/OSX)
     AC_DEFINE(HAVE_DARWIN,1,[define if compiling for Apple Darwin OSX])
     have_darwin=yes
     ;;
     *)
     echo "[!] Your system architecture is not supported by FreeJ"
     echo "[!] if you are interested in porting FreeJ to your architecture"
     echo "[!] you are very welcome to contact me <jaromil@dyne.org>"
     exit 0
     ;;
esac

AC_PROG_MAKE_SET

dnl ==============================================================
dnl Setup for automake
dnl ==============================================================

AM_SANITY_CHECK
AM_INIT_AUTOMAKE(FreeJ, 0.5.1)
AM_CONFIG_HEADER(config.h)


dnl ==============================================================
dnl Check for tools
dnl ==============================================================
AC_PROG_CC
AC_PROG_CXX

dnl Check for dlopen support
AC_LIBTOOL_DLOPEN
AC_DISABLE_STATIC

AM_PROG_LIBTOOL

dnl ==============================================================
dnl Add the local include path and some flags
dnl ==============================================================
CFLAGS="-g -Wall -O4 -pipe -D_REENTRANT -fomit-frame-pointer \
	   -mcpu=${host_cpu} -finline-functions -ffast-math -funroll-loops"
LIBS="-lpthread -lm -ldl -lpng"

dnl Checks for header files.
AC_HEADER_STDC

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl ==============================================================
dnl Checks for library functions.
dnl ==============================================================
AC_CHECK_FUNCS(select malloc mmap ioctl)

dnl ==============================================================
dnl Check for SDL
dnl ==============================================================
SDL_VERSION=1.2.0
AM_PATH_SDL($SDL_VERSION,
            :,
	AC_MSG_ERROR([*** SDL version $SDL_VERSION not found!])
           )
AC_SUBST(SDL_LIBS)

dnl AC_HEADER_DIRENT

dnl =====================================================
dnl CHECK if is wanted and there is AVILIB
dnl =====================================================
AVI_LIBS=""
AC_AVIFILE(0.7.18)
if test -z $HAVE_AVIFILE; then
echo "Error in avifile check, HAVE_AVIFILE is not declared"
fi
AC_ARG_ENABLE(avi,[  --enable-avi            compile with Avi Layer linking avifile (auto)], [
# to this when --enable-avi is selected (check the value!)
if test x$HAVE_AVIFILE = xyes -a ! x$enableval = xno; then
  AC_DEFINE(WITH_AVIFILE,1,
            [define if linking to avifile library for AVI layer playback])
  have_avilayer=yes
  AC_SUBST(AVIFILE_LIBS)
fi], [ 
# to this by default, when --enable-avi is not selected
if test x$HAVE_AVIFILE = xyes; then
  AC_DEFINE(WITH_AVIFILE,1,
            [define if linking to avifile library for AVI layer playback])
  have_avilayer=yes
  AC_SUBST(AVIFILE_LIBS)
fi])

dnl ==============================================================
dnl CHECK if there is FREETYPE2
dnl ==============================================================
AC_CHECK_FT2(9.0.0)

dnl ==============================================================
dnl CHECK if there is VIDEO4LINUX
dnl ==============================================================
AC_CHECK_HEADER(linux/videodev.h)
if test "$ac_cv_header_linux_videodev_h" = "yes"; then
HAVE_V4L=yes
fi
AC_ARG_ENABLE(v4l,[  --enable-v4l            compile with Video4Linux Layer (autodetect)], [
if test x$HAVE_V4L = xyes -a ! x$enableval = xno; then
   AC_DEFINE(WITH_V4L,1,[define if compiling video4linux layer])
   have_video4linux=yes
fi], [
if test x$HAVE_V4L = xyes; then
   AC_DEFINE(WITH_V4L,1,[define if compiling video4linux layer])
   have_video4linux=yes
fi])



dnl ==============================================================
dnl link with memory debugging library dmalloc
dnl ==============================================================
AC_CHECK_HEADER(dmalloc.h)
if test "$ac_cv_header_dmalloc_h" = "yes"; then

AC_MSG_CHECKING(if malloc debugging is wanted)
AC_ARG_WITH(dmalloc,
[  --with-dmalloc          use dmalloc, as in
                          ftp://ftp.letters.com/src/dmalloc/dmalloc.tar.gz],
[if test "$withval" = yes; then
  AC_MSG_RESULT(yes)
  AC_DEFINE(WITH_DMALLOC,1,
            [Define if using the dmalloc debugging malloc package])
  LIBS="$LIBS -ldmallocthcxx"
  DMALLOCC="dmallocc.cpp"
  have_dmalloc=yes
else
  AC_MSG_RESULT(no)
  DMALLOCC=""
  have_dmalloc=no
fi], [AC_MSG_RESULT(no)])

fi

CXXFLAGS="$CFLAGS $SDL_CFLAGS $AVIFILE_CXXFLAGS $FT2_CFLAGS"
CFLAGS="$CFLAGS $SDL_FLAGS"
#echo "LIBS are $LIBS"
#AC_SUBST(LIBS)


AC_OUTPUT([
Makefile
src/Makefile
src/include/Makefile
filters/Makefile
filters/delaygrab/Makefile
filters/absdiff/Makefile
filters/rotozoom/Makefile
filters/simura/Makefile
filters/baltan/Makefile
filters/vertigo/Makefile
filters/transform/Makefile
filters/blurzoom/Makefile
filters/edge/Makefile
filters/backlight/Makefile
filters/blur/Makefile
filters/ripple/Makefile
filters/water/Makefile
filters/cartoon/Makefile
filters/nervous/Makefile
])

echo
echo
echo "== ready to compile $PACKAGE $VERSION for ${host} (kernel $uname$uversion)"
echo " = SDL v${SDL_VERSION}: $SDL_LIBS"
echo " = FreeType2: $FT2_LIBS"

if test x$have_avilayer = xyes; then
  echo " = AviFile and DivX4linux: $AVIFILE_LIBS"
fi

echo " = Misc libraries: $LIBS"

echo "============================== now type make, may the source be with you!"
echo
